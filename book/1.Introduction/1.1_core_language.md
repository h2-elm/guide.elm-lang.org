
# Core Language

  - **values** 
  - **functions**

## Values

The smallest building block in Elm is called a **value**. This includes things like `42`, `True`, and `"Hello!"`.

Let's start by looking at numbers:

```
> 1+1
2

>
```

It is much more common to work with **strings** like this:

```
> "hello"
"hello"

> "butter" ++ "fly"
"butterfly"
```

Try putting some strings together with the `(++)` operator.

These primitive values get more interesting when we start writing functions to transform them!

> **Note:** You can learn more about operators like [`(+)`](https://package.elm-lang.org/packages/elm/core/latest/Basics#+) and [`(/)`](https://package.elm-lang.org/packages/elm/core/latest/Basics#/) and [`(++)`](https://package.elm-lang.org/packages/elm/core/latest/Basics#++) in the documentation for the [`Basics`](https://package.elm-lang.org/packages/elm/core/latest/Basics) module. It is worth reading through all the docs in that package at some point!


## Functions

A **function** is a way to transform values. Take in one value, and produce another.

For example, here is a `greet` function that takes in a name and says hello:

```elm
> greet name =
|  "Hello " ++ name ++ "!"
|

> greet "Alice"
"Hello Alice!"
```

How about a `madlib` function that takes _two_ arguments?

```elm
> madlib animal adjective =
|  "The ostentatious " ++ animal ++ " wears " ++ adjective ++ " shorts."
|
<function>

> madlib "cat" "ergonomic"
"The ostentatious cat wears ergonomic shorts."

> madlib ("butter" ++ "fly") "metallic"
"The ostentatious butterfly wears metallic shorts."
```

Notice how we used parentheses to group `"butter" ++ "fly"` together in the second example. Each argument needs to be a primitive value like `"cat"` or it needs to be in parentheses!

> **Note:** People coming from languages like JavaScript may be surprised that functions look different here:
>
>     madlib "cat" "ergonomic"                  -- Elm
>     madlib("cat", "ergonomic")                // JavaScript
>
>     madlib ("butter" ++ "fly") "metallic"      -- Elm
>     madlib("butter" + "fly", "metallic")       // JavaScript
>
> This can be surprising at first, but this style ends up using fewer parentheses and commas. It makes the language feel really clean and minimal once you get used to it!


## If Expressions

When you want to have conditional behavior in Elm, you use an if-expression.

Let's make a new `greet` function that is appropriately respectful to president Abraham Lincoln:

```elm
> greet name =
|  if name == "Abraham Lincoln" then
|    "Greetings Mr. President!"
|  else
|    "Hey!"
|

> greet "Tom"
"Hey!"

> greet "Abraham Lincoln"
"Greetings Mr. President!"
```


## Lists

Lists hold a sequence of values of the same type.

Here are a few examples that use functions from the [`List`][list] module:

[list]: https://package.elm-lang.org/packages/elm/core/latest/List

```elm
> names = [ "Alice", "Bob", "Chuck" ]
["Alice","Bob","Chuck"]

> List.isEmpty names
False

> List.length names
3

> List.reverse names
["Chuck","Bob","Alice"]

> numbers = [4,3,2,1]
[4,3,2,1]

> List.sort numbers
[1,2,3,4]

> increment n =
|  n + 1
|

> List.map increment numbers
[5,4,3,2]
```

Try making your own list and using functions like `List.length` ⬆️


## Tuples

A tuple can hold two or three values, and each value can have any type. A common use is if you need to return more than one value from a function.

```elm
isGoodName name =
  if String.length name <= 20 then
	(True, "name accepted!")
  else
	(False, "name was too long; please limit it to 20 characters")

> isGoodName "Tom"
(True,"name accepted!")

> point = (3, 4)
(3,4)

> Tuple.first point
3

> Tuple.second point
4

> Tuple.map2 (+) (1, 2) (3, 4)
(4,6)

> Tuple.map3 (\x y z -> x + y + z) (1, 2, 3) (4, 5, 6) (7, 8, 9)
(12,15,18)
```

This can be quite handy, but when things start becoming more complicated, it is often best to use records instead of tuples.


## Records

A **record** can hold many values, and each value is associated with a name.

Here is a record that represents British economist John A. Hobson:

```elm
> john =
|  { first = "John"
|  , last = "Hobson"
|  , age = 81
|  }
|
{ age = 81, first = "John", last = "Hobson" }

> john.last
"Hobson"
```

We defined a record with three **fields**.

Try accessing other fields like `john.age`. 

You can also access record fields by using a "field access function" like this:

```elm
> john = { first = "John", last = "Hobson", age = 81 }
{ age = 81, first = "John", last = "Hobson" }

> .last john
"Hobson"

> List map .last [john,john,john]
["Hobson","Hobson","Hobson"]
```

It is often useful to **update** values in a record:

```elm
> john = { first = "John", last = "Hobson", age = 81 }
{ age = 81, first = "John", last = "Hobson" }

> { john | last = "Adams" }
{ age = 81, first = "John", last = "Adams" }

> { john | age = 22 }
{ age = 22, first = "John", last = "Hobson" }
```

If you wanted to say these expressions out loud, you would say something like, "I want a new version of John where his last name is Adams" or "john where the age is 22".

Notice that when we update some fields of `john` we create a whole new record. It does not overwrite the existing one. Elm makes this efficient by sharing as much content as possible. If you update one of ten fields, the new record will share the nine unchanged values.

So a function to update ages might look like this:

```elm
> celebrateBirthday person =
|  { person | age = person.age + 1 }
|

> john = { first = "John", last = "Hobson", age = 81 }
{ age = 81, first = "John", last = "Hobson" }

> celebrateBirthday john
{ age = 82, first = "John", last = "Hobson" }
```

Updating record fields like this is really common, so we will see a lot more of it in the next section!
